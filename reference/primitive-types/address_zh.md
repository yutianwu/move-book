---
title: '地址 | 参考'
description: ''
---

# 地址

`address`是Move中的内置类型，用于表示存储中的位置（有时称为账户）。`address`值是一个256位（32字节）标识符。Move使用地址来区分[模块](./../modules_zh)包，其中每个包都有自己的地址和模块。Move的特定部署也可能将`address`值用于[存储](./../abilities_zh#key)操作。

> 对于Sui，`address`用于表示"账户"，也通过强类型包装器表示对象（使用`sui::object::UID`和`sui::object::ID`）。

虽然`address`在底层是一个256位整数，但Move地址是故意不透明的——它们不能从整数创建，不支持算术运算，也不能被修改。Move的特定部署可能有`native`函数来启用其中一些操作（例如，从字节`vector<u8>`创建`address`），但这些不是Move语言本身的一部分。

虽然存在运行时地址值（`address`类型的值），但它们_不能_用于在运行时访问模块。

## 地址及其语法

地址有两种形式：命名或数字。命名地址的语法遵循Move中任何命名标识符的相同规则。数字地址的语法不限于十六进制编码值，任何有效的[`u256`数值](./integers_zh)都可以用作地址值，例如，`42`、`0xCAFE`和`10_000`都是有效的数字地址字面量。

为了区分地址是否在表达式上下文中使用，使用地址时的语法根据使用的上下文而不同：

- 当地址用作表达式时，地址必须以`@`字符为前缀，即[`@<numerical_value>`](./integers_zh)或`@<named_address_identifier>`。
- 在表达式上下文之外，地址可以不带前导`@`字符书写，即[`<numerical_value>`](./integers_zh)或`<named_address_identifier>`。

一般来说，您可以将`@`视为一个操作符，它将地址从命名空间项转换为表达式项。

## 命名地址

命名地址是一个功能，允许在使用地址的任何地方使用标识符代替数值，而不仅仅是在值级别。命名地址在Move包中声明和绑定为顶级元素（在模块和脚本之外），或作为参数传递给Move编译器。

命名地址仅存在于源语言级别，在字节码级别将完全替换为它们的值。因此，应该通过模块的命名地址而不是在编译期间分配给命名地址的数值来访问模块和模块成员。所以虽然`use my_addr::foo`等价于`use 0x2::foo`（如果`my_addr`被分配为`0x2`），但最佳实践是始终使用`my_addr`名称。

### 示例

```move
// 简写形式
// 0x0000000000000000000000000000000000000000000000000000000000000001
let a1: address = @0x1;
// 简写形式
// 0x0000000000000000000000000000000000000000000000000000000000000042
let a2: address = @0x42;
// 简写形式
// 0x00000000000000000000000000000000000000000000000000000000DEADBEEF
let a3: address = @0xDEADBEEF;
// 简写形式
// 0x000000000000000000000000000000000000000000000000000000000000000A
let a4: address = @0x0000000000000000000000000000000A;
// 将`a5`分配为命名地址`std`的值
let a5: address = @std;
// 任何有效的数值都可以用作地址
let a6: address = @66;
let a7: address = @42_000;

module 66::some_module {   // 不在表达式上下文中，所以不需要@
    use 0x1::other_module; // 不在表达式上下文中，所以不需要@
    use std::vector;       // 可以使用命名地址作为命名空间项
    ...
}

module std::other_module {  // 声明模块时可以使用命名地址
    ...
}
```